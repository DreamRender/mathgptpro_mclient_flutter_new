plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

//使用Glide
apply plugin: 'kotlin-kapt'

android {
    namespace "com.mathgptpro.mclient"
    ndkVersion ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.mathgptpro.mclient"
        compileSdk 34
        minSdkVersion 21
        targetSdkVersion 34
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        //解决Cannot fit requested classes in a single dex file问题
        multiDexEnabled true
    }

    //签名配置
    //要写到使用签名配置的前面
    signingConfigs {
        debug {
            storeFile file('../../res/key/debug-release-key.keystore')
            storePassword "vIq6Fr5cOUsCsKr0"
            keyAlias "key0"
            keyPassword "vIq6Fr5cOUsCsKr0"
        }

        release {
            storeFile file('../../res/key/debug-release-key.keystore')
            storePassword "vIq6Fr5cOUsCsKr0"
            keyAlias "key0"
            keyPassword "vIq6Fr5cOUsCsKr0"
        }
    }

    buildTypes {

        debug {
            signingConfig signingConfigs.debug
        }

        profile {
            signingConfig signingConfigs.debug
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //解决split abi会造成版本号不一致的问题
    //参考：https://developer.android.com/studio/build/configure-apk-splits#configure-abi-split
    //参考：https://developer.android.com/google/play/publishing/multiple-apks#VersionCodes
    //参考：https://github.com/flutter/flutter/issues/39817
    //代码来源：https://github.com/flutter/flutter/issues/49544
    project.android.applicationVariants.configureEach { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride = variant.versionCode
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    //解决Cannot fit requested classes in a single dex file问题
    implementation 'com.android.support:multidex:1.0.3'
    //使用Glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
}
